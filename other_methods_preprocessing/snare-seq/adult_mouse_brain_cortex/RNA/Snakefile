configfile: 'config.json'

accessions = ['SRR9672089', 'SRR9672091', 'SRR9672093', 'SRR9672095',
              'SRR9672097', 'SRR9672099', 'SRR9672101', 'SRR9672103',
              'SRR9672105', 'SRR9672107', 'SRR9672109', 'SRR9672111']

rule all:
    input:
        expand('raw_data/{srr}.sra', srr=accessions),
        expand('raw_data/{srr}_1.fastq.gz', srr=accessions),
        expand('raw_data/{srr}_2.fastq.gz', srr=accessions),
        expand('{srr}/outs/Solo.out/Gene/filtered/barcodes.tsv', srr=accessions),
        expand('{srr}/outs/Solo.out/Gene/filtered/features.tsv', srr=accessions),
        expand('{srr}/outs/Solo.out/Gene/filtered/matrix.mtx', srr=accessions),
        expand('{srr}/outs/Aligned.sortedByCoord.out.bam', srr=accessions),
        expand('{srr}/outs/Solo.out/Gene/filtered/metrics.csv', srr=accessions),
        'metrics.csv'

rule downloadSra:
    output:
        'raw_data/{srr}.sra'
    shell:
        ''' prefetch {wildcards.srr} -o {output}
        '''

rule dumpFastq:
    input:
        'raw_data/{srr}.sra'
    output:
        'raw_data/{srr}_1.fastq.gz',
        'raw_data/{srr}_2.fastq.gz'
    threads: 8
    shell:
        ''' fasterq-dump --split-3 -e {threads} -O raw_data {input}
            gzip raw_data/{wildcards.srr}_1.fastq
            gzip raw_data/{wildcards.srr}_2.fastq
       '''

rule star:
    input:
        pg=config['star_bin'],
        cb='raw_data/{srr}_1.fastq.gz',
        cdna='raw_data/{srr}_2.fastq.gz'
    output:
        '{srr}/outs/Solo.out/Gene/filtered/barcodes.tsv',
        '{srr}/outs/Solo.out/Gene/filtered/features.tsv',
        '{srr}/outs/Solo.out/Gene/filtered/matrix.mtx',
        '{srr}/outs/Aligned.sortedByCoord.out.bam'
    params:
        idx=config['star_idx']
    threads: 40
    shell:
        ''' {input.pg} --genomeDir {params.idx} \
            --outFileNamePrefix ./{wildcards.srr}/outs/ \
            --readFilesCommand zcat --readFilesIn {input.cdna} {input.cb} \
            --soloCBstart 1 --soloCBlen 12 --soloUMIstart 13 --soloUMIlen 8 \
            --soloBarcodeReadLength 0 \
            --soloType CB_UMI_Simple --clip3pNbases 15 \
            --soloCBwhitelist None \
            --soloCellFilter EmptyDrops_CR --runThreadN {threads} \
            --outSAMattributes CB UB --outSAMtype BAM SortedByCoordinate
        '''

rule outputMetrics:
    input:
        bc='{srr}/outs/Solo.out/Gene/filtered/barcodes.tsv',
        mtx='{srr}/outs/Solo.out/Gene/filtered/matrix.mtx'
    output:
        '{srr}/outs/Solo.out/Gene/filtered/metrics.csv'
    shell:
        ''' ../../../scripts/output_qc_mtx {input.mtx} {input.bc} {output}
        '''

rule collectAllMetrics:
    input:
        expand('{srr}/outs/Solo.out/Gene/filtered/metrics.csv', srr=accessions)
    output:
        'metrics.csv'
    shell:
        ''' cat <(echo "cell,nCounts,nFeatures") \
                <(cat {input} | sed '/cell,nCounts,nFeatures/d') \
            > {output}
        '''
